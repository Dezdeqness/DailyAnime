plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.ksp)
    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
    alias(libs.plugins.arturbosch.detekt)
    alias(libs.plugins.google.firebase.crashlytics)
    alias(libs.plugins.compose.compiler)
    id 'androidx.navigation.safeargs'
}

android {
    compileSdk 34

    defaultConfig {
        applicationId "com.dezdeqness"
        minSdk 24
        targetSdk 34
        versionCode System.getenv("RELEASE_VERSION_CODE") as Integer ?: 999
        versionName "0.4.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            def props = new Properties()
            if (rootProject.file("build.jenkins.properties").exists()) {
                props.load(new FileInputStream(rootProject.file("build.jenkins.properties")))
            } else {
                props.load(new FileInputStream(rootProject.file("build.properties")))
            }
            keyAlias props['keystore.key.alias']
            keyPassword props['keystore.key.password']
            storeFile file(props['keystore.release'])
            storePassword props['keystore.password']
        }
    }

    buildFeatures {
        viewBinding = true
    }
    buildFeatures {
        compose = true
        buildConfig true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = libs.versions.kotlinCompilerExtensionVersion
    }

    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            proguardFiles 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            debuggable true
            applicationIdSuffix '.debug'
            proguardFiles 'proguard-rules.pro'
        }
        qa {
            initWith debug
            applicationIdSuffix '.qa'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility versions.javaVersion
        targetCompatibility versions.javaVersion
    }
    namespace 'com.dezdeqness'

}
detekt {
    toolVersion = "1.23.0"
    config = files("${project.rootDir.absolutePath}/config/detekt/detekt.yml")
    buildUponDefaultConfig = true
}

dependencies {

    firebase()
    zoomable()
    mock()
    chucker()
    room()
    dagger()
    network()
    glide()
    coroutines()
    lifecycle()
    material()
    constraintLayout()
    core()
    appcompat()
    fragment()
    unitTesting()
    androidTesting()
    navigation()
    swipe()
    composer()
    coil()
    implementation(libs.core.ui)
    implementation(libs.core)
    implementation(libs.pod.core)

    implementation project(path: ':advanced_recycler')
    implementation project(path: ':data')
    implementation project(path: ':domain')
    implementation("com.github.terrakok:cicerone:7.1")

}